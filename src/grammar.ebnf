Stmt              := EmptyStmt ';'
                   | BlockStmt ';'
                   | PackageStmt ';'
                   | ImportStmt ';'
                   | DeferStmt ';'
                   | ReturnStmt ';'
                   | BreakStmt ';'
                   | ContinueStmt ';'
                   | FallthroughStmt ';'
                   | ForeignStmt ';'
                   | IfStmt ';'
                   | WhenStmt ';'
                   | ForStmt ';'
                   | SwitchStmt ';'
                   | UsingStmt ';'
                   | DeclStmt ';'
                   | AssignStmt ';'

EmptyStmt         := ';'
BlockStmt         := '{' Stmt* '}'
PackageStmt       := 'package' Ident
ImportStmt        := 'import' Ident? StringExpr
BreakStmt         := 'break' Ident?
ContinueStmt      := 'continue' Ident?
FallthroughStmt   := 'fallthrough'
ForeignImportStmt := 'foreign' 'import' Ident? StringLit
                   | 'foreign' 'import' Ident? '{' (Expr ',')+ '}'

ExprStmt          := Expr
WhenStmt          := 'when' Expr BlockStmt ('else' BlockStmt)?
DeferStmt         := 'defer' Stmt
ReturnStmt        := 'return' (Expr (',' Expr)*)?
UsingStmt         := 'using' Expr
DeclStmt          := Expr (',' Expr)* ':' Type? (('=' | ':') Expr (','  Expr)*)?
AssignStmt        := Expr (',' Expr)* AssignOp Expr (',' Expr)*
Expr              := IfExpr
                   | WhenExpr
                   | DerefExpr
                   | OrReturnExpr
                   | OrBreakExpr
                   | IdentExpr
                   | UndefExpr
                   | ContextExpr
                   | RangeExpr
                   | IntExpr
                   | FloatExpr
                   | StringExpr
                   | ImaginaryExpr
                   | CompoundExpr
                   | SelectorExpr
                   | CastExpr
                   | TypeExpr
IfExpr            := Expr 'if' Expr 'else' Expr
                   | Expr '?' Expr ':' Expr
WhenExpr          := Expr 'when' Expr 'else' Expr
DerefExpr         := Expr '^'
OrReturnExpr      := Expr 'or_return'
OrBreakExpr       := Expr 'or_break'
IdentExpr         := Ident
UndefExpr         := '---'
ContextExpr       := 'context'
RangeExpr         := Expr '..=' Expr
                   | Expr '..<' Expr
SliceExpr         := Expr '[' Expr? ':' Expr? ']'
IntExpr           := IntLit
FloatExpr         := FloatLit
StringExpr        := StringLit
ImaginaryExpr     := ImaginaryLit
CompoundExpr      := '{' Field (',' Field)* '}'
SelectorExpr      := '.' Ident
AccessExpr        := Expr '.' Ident
CastExpr          := '(' Type ')' Expr
                   | 'auto_cast' Expr
TypeExpr          := Type

Type              := UnionType
                   | StructType
                   | EnumType
                   | PtrType
                   | MultiPtrType
                   | SliceType
                   | DynArrayType
                   | ArrayType
                   | MapType
                   | MatrixType
                   | NamedType
                   | ParamType
                   | ParenType
                   | DistinctType
                   | TypeIDType
UnionType          := 'union' '{' (Type ',')* Type? '}'
EnumType           := 'enum' Type? '{' Field (',' Field)* '}'
PtrType            := '^' Type
MultiPtrType       := '[' '^' ']' Type
SliceType          := '[' ']' Type
ArrayType          := '[' (Expr | '?') ']' Type
DynArrayType       := '[' 'dynamic' ']' Type
MapType            := 'map' [' Type ']' Type
MatrixType         := 'matrix' '[' Expr ',' Expr ']' Type
NamedType          := Ident ('.' Ident)?
ParenType          := '(' Type ')'
DistinctType       := 'distinct' Type
TypeIDType         := 'typeid'

AttributeList      := '@' Field
                    | '@' '(' Field (',' Field)* ')'
DirectiveList      := Directive*
Field              := Expr ('=' Expr)?
Directive          := '#' Ident '(' Expr (',' Expr)* ')'
